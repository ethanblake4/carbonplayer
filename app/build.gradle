apply plugin: 'com.android.application'

apply plugin: 'com.google.protobuf'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

apply plugin: 'realm-android'

if(new File("carbonplayer.properties").exists()) {
    Properties props = new Properties()
    props.load(new FileInputStream(new File("carbonplayer.properties")))
    android {
        signingConfigs {
            config {
                keyAlias 'Carbon'
                keyPassword props['keystore.keypassword']
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
            }
        }
        compileSdkVersion 28
        buildToolsVersion "28.0.3"
        defaultConfig {
            System.out.println(signingConfigs.config)
            applicationId "com.carbonplayer"
            minSdkVersion 21
            targetSdkVersion 28
            versionCode 6
            versionName "0.2.1"
            buildConfigField "boolean", "RELEASE", "false"
            signingConfig signingConfigs.config
            multiDexEnabled true
        }
        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.config
            }
            debug {
                //minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                signingConfig signingConfigs.config
            }
        }

        flavorDimensions "version"
        productFlavors {
            prod {
                buildConfigField "boolean", "RELEASE", "true"
                dimension "version"
            }
            dev {
                signingConfig signingConfigs.config
                dimension "version"
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        dexOptions {
            // Prevent OutOfMemory with MultiDex during the build phase
            javaMaxHeapSize "2g"
        }

        sourceSets {
            main {
                proto {
                    srcDir 'src/main/proto/'
                }
                java {
                    srcDir 'src/main/java'
                }
            }
        }
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:3.0.0"
        }

        plugins {
            lite {
                artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
            }
        }

        generateProtoTasks {
            all().each {
                task -> task.builtins {
                    remove java
                }
                task.plugins {
                    lite { }
                }
            }
        }
    }
}

repositories {
    maven {url "https://clojars.org/repo/"}
    maven { url 'https://github.com/WickeDev/stetho-realm/raw/master/maven-repo' }
    mavenCentral()
    google()
    jcenter()
}

ext {
    supportLibVersion = '28.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.mediarouter:mediarouter:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'

    implementation 'androidx.cardview:cardview:1.0.0'

    implementation 'androidx.core:core-ktx:1.0.1'

    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'

    implementation 'com.bluelinelabs:conductor:2.1.4'
    implementation 'com.bluelinelabs:conductor-support:2.1.4'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation 'com.uphyca:stetho_realm:2.2.2'

    implementation 'com.google.android.exoplayer:exoplayer:2.7.3'
    implementation 'com.google.android.gms:play-services-auth:15.0.1'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    protobuf 'com.google.protobuf:protobuf-java:3.4.0'
    implementation 'com.google.guava:guava:23.0-android'

    implementation 'com.jakewharton:butterknife:9.0.0-rc2'
    kapt 'com.jakewharton:butterknife-compiler:9.0.0-rc2'
    implementation 'com.jakewharton.timber:timber:4.6.0'

    implementation 'com.github.bumptech.glide:glide:4.7.1'
    implementation('com.github.bumptech.glide:okhttp3-integration:4.7.1') {
        exclude group: 'glide-parent'
    }

    implementation 'com.sothree.slidinguppanel:library:3.4.0'

    implementation('com.github.florent37:glidepalette:2.1.0') {
        exclude group: 'com.github.bumptech.glide'
    }

    implementation 'com.mikhaellopez:circularprogressbar:1.1.1'

    implementation 'com.github.ksoichiro:android-observablescrollview:1.6.0'

    implementation 'com.otaliastudios:nestedscrollcoordinatorlayout:1.0.1'

    implementation 'com.evernote:android-job:1.2.0'

    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    implementation 'frankiesardo:icepick:3.2.0'
    kapt 'frankiesardo:icepick-processor:3.2.0'

    implementation 'org.parceler:parceler-api:1.1.10'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.8'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    implementation 'com.github.vicpinm:krealmextensions:2.1.0'

    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.1.0'

    implementation 'com.squareup.moshi:moshi:1.5.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.5.0'

    implementation 'de.hdodenhof:circleimageview:2.2.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'com.jakewharton:process-phoenix:2.0.0'

    implementation 'com.karumi:headerrecyclerview:1.1.0'
}

apply plugin: 'com.google.gms.google-services'

android {
    buildToolsVersion '28.0.3'
}

androidExtensions {
    experimental = true
}